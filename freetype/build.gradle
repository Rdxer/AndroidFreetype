apply plugin: 'com.android.library'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    /*externalNativeBuild {
        ndkBuild {
            ndkBuild {
                path "src/main/jni/Android.mk"
            }
        }
    }*/
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.0.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

ext {
    // 从Github上clone下来的项目的本地地址
    GITHUB_REPO_PATH = "./../"
    PUBLISH_GROUP_ID = 'com.xian'
    PUBLISH_ARTIFACT_ID = 'freestype'
    PUBLISH_VERSION = '1.0.1'
}


apply plugin: 'maven-publish'

// 源代码Jar一起打包，便于引入查看源码
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.getSrcDirs()
}
artifacts {
    archives androidSourcesJar
}

publishing {
    repositories {
        maven {
            def deployPath = file(project.GITHUB_REPO_PATH)
            url "file://${deployPath.absolutePath}" // 仓库地址
        }
    }
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                afterEvaluate { artifact(tasks.getByName("bundleReleaseAar")) }

                groupId project.PUBLISH_GROUP_ID
                artifactId project.PUBLISH_ARTIFACT_ID
                version project.PUBLISH_VERSION
                artifact androidSourcesJar
            }
        }
    }
}